import os
import posixpath
Import('env')
env = env.Clone()

InstBin = []

binaries = Split("""
	mamaftmemberc
	mamainboxc
	mamalistenc
	mamamultisubscriberc
	mamapublisherc
	mamaproxyc
	mamasubscriberc
	mamasymbollistsubscriberc
	mamalistencachedc
""")

libs = []
libs.append('libmamac%s' % ( env['suffix'] ))

libpath = []
libpath.append('%s/%s/wombatmsg/c_cpp/src/c' % ( env['blddir'], env['build'] ))
libpath.append('$libdir')

env.Append( 
	LIBS 		= libs,
	LIBPATH 	= libpath,
)

for b in binaries:
	example = env.Program(b, '%s.c' % (b) ) 
	if env['vsver'] >= 8:
		env.AddPostAction( example, 'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;1')
	InstBin.append( example )

Alias( 'install', env.Install('$bindir',InstBin) )
